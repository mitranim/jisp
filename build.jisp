(= fs   (require "fs")
   args (get (require "yargs") argv))

; Accepted arguments:
  ; no arguments -- rebuild dev with dev
  ; -r           -- rebuild dev with stable
  ; -s           -- rebuild stable with dev

(= files `("index"
           "functions"
           "utils"
           "operators"
           "tokenise"
           "lex"
           "parse"
           "register"
           "repl"
           "optparse"
           "command"
           "macros"
           "uniq"
           "jisp"
           "browser"))

(= filesBrowser `("utils"
                  "functions"
                  "operators"
                  "tokenise"
                  "lex"
                  "parse"
                  "macros"
                  "uniq"
                  "jisp"
                  "browser"))

(def buildFile compiler filename srcDir destDir (do
     (= srcPath   (+ srcDir filename ".jisp")
        destPath  (+ destDir filename ".js")
        src       (fs.readFileSync srcPath "utf8"))
     (prn (+ "-- compiling " filename "..."))
     (= js (compiler.compile src))
     (fs.writeFileSync destPath js "utf8")))

(def build jisp srcDir destDir (do
     (prn "-- building...")
     (for filename files (buildFile jisp filename srcDir destDir))
     (prn "-- if you're reading this, compilation has succeeded!")))

(if args.r
  (do
    ; Load compiler
    (prn "-- trying to load stable compiler...")
    (= jisp      (require "./lib/jisp/jisp"))
    ; Build with stable
    (prn "-- rebuilding dev with stable...")
    (build jisp "./src/" "./dev/jisp/"))
  (elif args.s (do
    ; Load compiler
    (prn "-- trying to load unstable compiler...")
    (= jisp_unstable (require "./dev/jisp/jisp"))
    ; Build with unstable
    (prn "-- trying to build lib with unstable compiler...")
    (build jisp_unstable "./src/" "./lib/jisp/")))
  (do
    ; Load compiler
    (prn "-- trying to load unstable compiler...")
    (= jisp_unstable (require "./dev/jisp/jisp"))
    ; Build with unstable
    (prn "-- trying to build dev with unstable compiler...")
    (build jisp_unstable "./src/" "./dev/jisp/")
    (prn "-- when testing the unstable version, recompile at least twice!")))

; Build the concatenated version for the browser
; Copied from the CoffeeScript cakefile

(= code "")

(for name filesBrowser
  (+= code (+
    "require['./" name "'] = (function() {
      var exports = {}, module = {exports: exports};
      " (fs.readFileSync (+ "./dev/jisp/" name ".js")) "
      return module.exports;
    })();")))

(= code (+
  "(function(root) {
    var jisp = function() {
      function require(path) { return require[path]; }
      " code "
      return require['./jisp'];
    }();
    root.jisp = jisp;
  }(this));"))

(fs.writeFileSync "./browser/jisp.js" code)
(prn "-- if you're reading this, concat build has succeeded!")

(try (do (fs.writeFileSync "./node_modules/jisp/browser/jisp.js" code)
         (prn "-- replaced the browser module in ./node_modules/jisp/browser/jisp.js for gh-pages testing")))
